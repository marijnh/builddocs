<<do
  var undocumentedProps = false
  if ($in.properties) for (var n in $in.properties) if (!hasDescription($in.properties[n])) undocumentedProps = true
>>

<<if $in.type == "Function">>
  <span class=fn>fn</span><<fntype $in>>
<<elif $in.type == "Array">>
  <<typeParens $in.typeArgs[0]>>[]
<<elif $in.type == "ReadonlyArray">>
  readonly <<typeParens $in.typeArgs[0]>>[]
<<elif $in.type == "tuple">>
  [<<for elt $in.typeArgs || []>><<if $i>>, <</if>><<type elt>><</for>>]
<<elif $in.type == "union">>
  <<for elt $in.typeArgs>><<if $i>> | <</if>><<type elt>><</for>>
<<elif $in.type == "intersection">>
  <<for elt $in.typeArgs>><<if $i>> & <</if>><<type elt>><</for>>
<<elif $in.type == "indexed">>
  <<for elt $in.typeArgs>><<if $i>>[<</if>><<type elt>><<if $i>>]<</if>><</for>>
<<elif $in.type == "conditional">>
  <<type $in.typeArgs[0]>> extends <<type $in.typeArgs[1]>> ? <<type $in.typeArgs[2]>> : <<type $in.typeArgs[3]>>
<<elif $in.type == "mapped">>
  {[<a id="<<t prefix + $in.key.id>>" href="#<<t prefix + $in.key.id>>"><<t $in.key.name>></a> in <<type $in.key.implements[0]>>]: 
  <<type $in.typeArgs[0]>>}
<<elif $in.type == "keyof">>
  keyof <<typeParens $in.typeArgs[0]>>
<<elif $in.type == "typeof">>
  typeof <<typeParens $in.typeArgs[0]>>
<<elif undocumentedProps>>
  <<do var needComma = false>>
  {<<for name, prop in $in.properties>>
     <<if !hasDescription(prop)>>
       <<if needComma>>, <</if>>
       <span class=prop><<t name>><<if prop.optional>>&#8288;?<</if>>: </span><<type prop>>
       <<do needComma = true>>
     <</if>>
   <</for>>}
<<else>>
  <<do var found = linkType($in)
       var cls = /^"/.test($in.type) ? "string" : /(^|\.)[A-Z]\w+$/.test($in.type) ? "type" : "prim" >>
  <<if found>><a href="<<t found>>"><</if>>
    <span class="<<t cls>>"><<t $in.type>></span>
  <<if found>></a><</if>>
  <<if $in.typeArgs>>&lt;<<for elt $in.typeArgs>><<if $i>>, <</if>><<type elt>><</for>>&gt;<</if>>
<</if>>
