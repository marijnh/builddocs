<<in {item, name, static}>>
<dt id="<<t prefix + item.id>>">
  <<if item.type == "Function" && !item.optional>>
    <code><<if item.abstract>>abstract <</if>><<if static>>static <</if>><<if item.readonly>>readonly <</if>><<if /\.constructor$/.test(item.id)>>new <</if>><strong><a href="#<<t prefix + item.id>>"><<t name>></a></strong><<fntype item>></code>
  <<else>>
    <code><<if item.abstract>>abstract <</if>><<if static>>static <</if>><<if item.readonly>>readonly <</if>><strong><a href="#<<t prefix + item.id>>"><<t name>></a></strong><<if item.type>>: <<type item>><</if>></code>
  <</if>>
</dt>

<dd>
  <<markdown item.description>>
  <<do let inList = false>>
  <<for name, prop in item.properties || {}>>
    <<if hasDescription(prop)>>
      <<if !inList>><dl><<do inList = true>><</if>>
      <<define {item: prop, name: name}>>
    <</if>>
  <</for>>
  <<for param item.params || []>>
    <<if hasDescription(param)>>
      <<if !inList>><dl><<do inList = true>><</if>>
      <<define {item: param, name: param.name}>>
    <</if>>
  <</for>>
  <<if item.returns && hasDescription(item.returns)>>
    <<if !inList>><dl><<do inList = true>><</if>>
    <<define {item: item.returns, name: "returns"}>>
  <</if>>
  <<if inList>></dl><</if>>
</dd>
